apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.11" // use latest
}

tasks.register("jacocoTestReport", JacocoReport) {
    dependsOn "testDevDebugUnitTest" // or test<YourBuildType>UnitTest

    reports {
        xml.required.set(true)
        html.required.set(true)
    }

    def fileFilter = [
            '**/R.class', '**/R$*.class',
            '**/BuildConfig.*', '**/Manifest*.*',
            '**/*Test*.*', '**/Hilt*.*',
            '**/*_Factory.class', '**/*_MembersInjector.class',
            '**/*Module*.*', '**/*Dagger*.*',
            '**/di/module/**'
    ]

    def debugTree = fileTree(
            dir: "$buildDir/tmp/kotlin-classes/devDebug",
            excludes: fileFilter
    )

    sourceDirectories.setFrom(files([
            "src/main/java",
            "src/main/kotlin"
    ]))

    classDirectories.setFrom(files([debugTree]))
    executionData.setFrom(fileTree(buildDir).include(
            "jacoco/testDevDebugUnitTest.exec"
    ))
}
